PROGRAMA →
	program id;
	DECLARAÇÕES_VARIÁVEIS
	DECLARAÇÕES_DE_SUBPROGRAMAS
	COMANDO_COMPOSTO
	.


DECLARAÇÕES_VARIÁVEIS →
	var LISTA_DECLARAÇÕES_VARIÁVEIS
	| ε


LISTA_DECLARAÇÕES_VARIÁVEIS →
	LISTA_DECLARAÇÕES_VARIÁVEIS LISTA_DE_IDENTIFICADORES: TIPO;
	| LISTA_DE_IDENTIFICADORES: TIPO;


LISTA_DE_IDENTIFICADORES →
	id
	| LISTA_DE_IDENTIFICADORES, id


TIPO →
	integer
	| real
	| boolean


DECLARAÇÕES_DE_SUBPROGRAMAS →
	DECLARAÇÕES_DE_SUBPROGRAMAS DECLARAÇÃO_DE_SUBPROGRAMA;
	| ε


DECLARAÇÃO_DE_SUBPROGRAMA →
	procedure id ARGUMENTOS;
	DECLARAÇÕES_VARIÁVEIS
	DECLARAÇÕES_DE_SUBPROGRAMAS
	COMANDO_COMPOSTO


ARGUMENTOS →
	(LISTA_DE_PARAMETROS)
	| ε


LISTA_DE_PARAMETROS →
	LISTA_DE_IDENTIFICADORES: TIPO
	| LISTA_DE_PARAMETROS; LISTA_DE_IDENTIFICADORES: TIPO


COMANDO_COMPOSTO →
	begin
	COMANDOS_OPCIONAIS
	end


COMANDOS_OPCIONAIS →
	LISTA_DE_COMANDOS
	| ε


LISTA_DE_COMANDOS →
	COMANDO
	| LISTA_DE_COMANDOS; COMANDO


COMANDO →
	VARIÁVEL := EXPRESSÃO
	| ATIVAÇÃO_DE_PROCEDIMENTO
	| COMANDO_COMPOSTO
	| if EXPRESSÃO then COMANDO PARTE_ELSE
	| while EXPRESSÃO do COMANDO


PARTE_ELSE →
	else COMANDO
	| ε


VARIÁVEL →
	id


ATIVAÇÃO_DE_PROCEDIMENTO →
	id
	| id (LISTA_DE_EXPRESSÕES)


LISTA_DE_EXPRESSÕES →
	EXPRESSÃO
	| LISTA_DE_EXPRESSÕES, EXPRESSÃO


EXPRESSÃO →
	EXPRESSÃO_SIMPLES
	| EXPRESSÃO_SIMPLES OP_RELACIONAL EXPRESSÃO_SIMPLES


EXPRESSÃO_SIMPLES →
	TERMO
	| SINAL TERMO
	| EXPRESSÃO_SIMPLES OP_ADITIVO TERMO


TERMO →
	FATOR
	| TERMO OP_MULTIPLICATIVO FATOR


FATOR →
	id
	| id(LISTA_DE_EXPRESSÕES)
	| NUM_INT
	| NUM_REAL
	| true
	| false
	| (EXPRESSÃO)
	| not FATOR


SINAL →
	+ | -


OP_RELACIONAL →
	= | < | > | <= | >= | <>


OP_ADITIVO →
	+ | - | or


OP_MULTIPLICATIVO →
	* | / | and